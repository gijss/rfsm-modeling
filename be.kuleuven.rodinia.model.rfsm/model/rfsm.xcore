@Ecore(invocationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL/Pivot",
settingDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL/Pivot", validationDelegates="http://www.eclipse.org/emf/2002/Ecore/OCL/Pivot")

package be.kuleuven.rodinia.model.rfsm

annotation "http://www.eclipse.org/emf/2002/Ecore/OCL/Pivot" as Pivot

/*
 * This Xcore metamodel of rFSM is based on the Ecore metamodel in the rFSM paper ('Coordinating Robotic Tasks and Systems with rFSM Statecharts' by M. Klotzbucher, H. Bruyninckx)
 */
 
@Ecore(constraints="defineOnlyOneRootNode")
//There is only one node that has no parent (ie: root node),
//a rFSM model is always contained within a state
@Pivot(defineOnlyOneRootNode="(Node.allInstances()->select(n | n.parent.oclIsUndefined ()))->size() = 1")
class Node {
	String name
	container State[0..1] parent opposite subnodes
}

@Ecore(constraints="defineInitialConnectorWhenComposite defineMaxOneInitialConnector onlyLeafStatesHaveDoo")
//Each composite state that is target of a transition, defines an initial connector.
//So the root composite state should define an initial connector??? (constraint is taken from rFSM paper)
@Pivot(defineInitialConnectorWhenComposite="self.subnodes->size() > 0 and Transition.allInstances()->exists(t | t.target=self) implies self.subnodes->select(c | c.oclIsTypeOf(Connector) and c.name='initial')->size() = 1")
//A composite state may define at most one initial connector.
@Pivot(defineMaxOneInitialConnector="(self.subnodes->size() > 0) implies (self.subnodes->select(c | c.oclIsTypeOf(Connector) and c.name='initial')->size() <= 1)")
//Only leaf-states can define a doo function. (not sure about this one)
@Pivot(onlyLeafStatesHaveDoo="(not self.doo.oclIsUndefined()) implies (self.subnodes->size() = 0)")
class State extends Node {
	contains Transition[] transitions opposite owner
	contains Node[] subnodes opposite parent
	contains Function[0..1] entry	//contains function in this example (original:refers)
	contains Function[0..1] doo	//contains function in this example (original:refers)
	contains Function[0..1] exit	//contains function in this example (original:refers)
}

class Connector extends Node {
	boolean public
	contains History[0..1] history
}

@Ecore(constraints="transitionOwner legalBoundaryCrossing maxOneTransitionFromInitialConnector noGuardFromInitialConnector")
//Transitions are owned by composite states and not by the states from which they originate
//It is required that transitions are owned by no state less nested than the transition LCA
@Pivot(transitionOwner="let lca : State = LCA(self.source, self.target) in self.owner = lca or isAncestor(self.owner, lca)") //example
//The transition target should point to a node less or equally nested than source node
//So the parent of the target node is an ancestor of the source node 
//and - IS THIS CORRECT? - a transition from parent to direct child is illegal
@Pivot(legalBoundaryCrossing="(self.target.parent <> self.source) and isAncestor(self.target.parent, self.source)")
//An initial connector can have only one outgoing transition
@Pivot(maxOneTransitionFromInitialConnector="(self.source.oclIsTypeOf(Connector) and self.source.name='initial') implies (Transition.allInstances()->select(t | t.source = self.source)->size()) <= 1")
//An outgoing transition from an initial connector can't have a guard function
@Pivot(noGuardFromInitialConnector="(self.source.oclIsTypeOf(Connector) and self.source.name='initial') implies guard.oclIsUndefined()")
class Transition {
	int priority_number = "0"
	container State[1] owner opposite transitions
	refers Node[1] source	//should only refer to node that is reachable from transition.owner or its subnodes->expressed by transitionOwner
	refers Node[1] target	//should only refer to node that is reachable from transition.owner or its subnodes ->expressed by transitionOwner
	contains Event[] events	opposite owner	//possible to add this logic to the guard function
	contains Function[0..1] guard	//contains function in this example (original:refers)
	contains Function[0..1] effect	//contains function in this example (original:refers)
	
	//is one ancestor of two?
	@Pivot(body="if (one = two) 
					then true
				else
					if (two.parent.oclIsUndefined ())
						then false
					else 
						if (two.parent = one)
							then true
						else 
							isAncestor(one, two.parent)
						endif
					endif
				endif")
	op Boolean isAncestor(Node one, Node two)
	
	//get least common ancestor of node one and two
	@Pivot(body="if isAncestor(one, two) and one.oclIsTypeOf(State)
					then one.oclAsType(State)
				else 
					if isAncestor(two, one) and two.oclIsTypeOf(State)
						then two.oclAsType(State)
					else
						LCA(one.parent,two.parent)
					endif
				endif")
	op State LCA(Node one, Node two)
}

//possible to add this logic to the guard function, which would be more generic
//this simple event-trigger-language is based on the Lua rFSM implementation
class Event {
	String eventliteral
	container Transition owner opposite events
}

//todo: use Lua grammar to parse function
class Function {
	String sourcecode
}

//how to model history behaviour?
//not implemented in Lua yet (I think)
class History {
	int depth
	boolean hot
}

//Behavioral constraints:
//exactly one leaf state is active and if a state is active, its parent is active too; so the set of active states is always transitively closed with respect to the parent relation.
//The higher the source state of a transition is located in the state machineâ€™s tree, the higher the priority of the transition.
//as soon as an enabled transition is found, the searching terminates and the transition is executed
//the program can't be in a connector, only in a state -> all guard functions on a compound transition should be true before traversing to the next state
